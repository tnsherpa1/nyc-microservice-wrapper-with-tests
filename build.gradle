buildscript {
	ext {
		springBootVersion = '2.0.1.RELEASE'
	}
	repositories {
		mavenCentral()
		jcenter()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath "com.avast.gradle:gradle-docker-compose-plugin:0.6.17"
		classpath "com.avast.gradle:gradle-docker-compose-plugin:0.6.17"
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'docker-compose'

dockerCompose {
	useComposeFiles = ['docker-compose.yml']
}
dockerCompose.isRequiredBy(test)

task waitForUsersAPI(type: Exec) {
	commandLine './wait-for-api.sh','http://localhost:8080/users', 'Users API'
}
task startUIDevServer(type: Exec) {
	workingDir './users-ui'
	commandLine 'npm', 'start'
}
test {
	dependsOn cleanTest
	dependsOn waitForUsersAPI
	dependsOn startUIDevServer

	testLogging {
		exceptionFormat "full"
		events "skipped", "passed", "failed"
		displayGranularity 2
	}
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
}

dependencies {
	compile('org.springframework.boot:spring-boot-starter-data-jpa')
	compile('org.springframework.boot:spring-boot-starter-web')
	testCompile('org.springframework.boot:spring-boot-starter-test')
	compileOnly 'org.projectlombok:lombok'
	runtime 'org.postgresql:postgresql'
	runtime 'org.springframework.boot:spring-boot-devtools'
	testCompile 'io.rest-assured:rest-assured:3.0.6'
}

