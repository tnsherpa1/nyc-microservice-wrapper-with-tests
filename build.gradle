buildscript {
	ext {
		springBootVersion = '1.5.10.RELEASE'
	}
	repositories {
		mavenCentral()
		jcenter()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath "com.avast.gradle:gradle-docker-compose-plugin:0.6.17"
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'docker-compose'

task runUsersAPITests(type: Exec) {
	workingDir './users-api'
	commandLine './gradlew', 'test'
}

task runProjectsAPITests(type: Exec) {
	workingDir './projects-api'
	commandLine './gradlew', 'test'
}

task waitForUsersAPI(type: Exec) {
	commandLine './wait-for-api.sh','http://localhost:8080/users', 'Users API'
}

task waitForProjectsAPI(type: Exec) {
	commandLine './wait-for-api.sh','http://localhost:8080/projects', 'Projects API'
}

test {
	dependsOn cleanTest
	dependsOn waitForUsersAPI
	dependsOn waitForProjectsAPI
	dependsOn runUsersAPITests
	dependsOn runProjectsAPITests

	testLogging {
		exceptionFormat "full"
		events "skipped", "passed", "failed"
		displayGranularity 2
	}

}

test.finalizedBy(composeDown)

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
}

dependencies {
	compile 'org.springframework.boot:spring-boot-starter-web'
	compile 'org.springframework.boot:spring-boot-starter-data-jpa'
	compileOnly 'org.projectlombok:lombok'

	testCompile 'org.springframework.boot:spring-boot-starter-test'
	testCompile 'io.rest-assured:rest-assured:3.0.6'
	testCompile 'org.seleniumhq.selenium:selenium-java:3.9.1'
	testCompile 'org.seleniumhq.selenium:selenium-api:3.9.1'
	testCompile 'org.seleniumhq.selenium:selenium-remote-driver:3.9.1'
	testCompile 'org.seleniumhq.selenium:selenium-chrome-driver:3.9.1'
	testCompile 'org.seleniumhq.selenium:selenium-support:3.9.1'
	testCompile 'com.codeborne:selenide:4.10.01'
	runtime 'org.postgresql:postgresql'
	runtime 'org.springframework.boot:spring-boot-devtools'
}

dockerCompose {
	useComposeFiles = ['docker-compose.yml']
}
dockerCompose.isRequiredBy(test)

